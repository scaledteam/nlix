#
# Created by scaled
#

@name fsreal

#ifdef fileRead()
PEnabled["fsreal", number] = 1

function fsreal() {
    switch (P["state", number]) {
        case 0,
            if (P["fs", table]:exists("path")) {
                switch (P["mode", string]) {
                    case "list",
                        P["state", number] = 1
                        break
                    
                    case "read",
                        P["state", number] = 3
                        break
                    
                    case "write",
                        P["state", number] = 5
                        break
                    
                    default,
                        out("Error: Unsupported action.\n")
                        end()
                }
            }
            else {
                out("Error: No path in fstab\n")
                end()
            }
            break
        
        # List
        case 1,
            if (fileCanList()) {
                fileList(P["fs", table]["path", string] + P["path", string] + "/")
                P["state", number] = 2
            }
            break
        
        case 2,
            if (fileLoadedList()) {
                out((fileReadList():concat("\n") + "\n"):replace(".txt\n", "\n"):sub(1, -2))
                out("\n")
                end()
            }
            break
        
        # Read
        case 3,
            if (fileCanLoad()) {
                fileLoad(P["fs", table]["path", string] + P["path", string] + ".txt")
                
                P["timeout", number] = systime() + 5
                P["state", number] = 4
            }
            break
        
        case 4,
            if (fileLoaded()) {
                out(fileRead())
                out("\n")
                end()
            }
            elseif (systime() > P["timeout", number]) {
                out("Error: Timeout.\n")
                end()
            }
            break
        
        # Write
        case 5,
            if (fileCanWrite() & wait()) {
                fileWrite(P["fs", table]["path", string] + P["path", string] + ".txt", in())
                out("")
                end()
            }
            break
    }
}
