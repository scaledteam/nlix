#
# Created by scaled
#

@name fsreal

#ifdef fileRead()
P2F["fsreal", string] = "fsreal"

function fsreal() {
    if (P["fs", table]:exists("path")) {
        switch (P["mode", string]) {
            case "list",
                switch (P["state", number]) {
                    case 0,
                        if (fileCanList()) {
                            fileList(P["fs", table]["path", string] + P["path", string] + "/")
                            P["state", number] = 1
                        }
                        break
                    
                    case 1,
                        if (fileLoadedList()) {
                            out((fileReadList():concat("\n") + "\n"):replace(".txt\n", "\n"))
                            end()
                        }
                        break
                }
                break
            
            case "read",
                switch (P["state", number]) {
                    case 0,
                        if (fileCanLoad()) {
                            fileLoad(P["fs", table]["path", string] + P["path", string] + ".txt")
                            
                            P["timeout", number] = systime() + 5
                            P["state", number] = 1
                        }
                        break
                    
                    case 1,
                        if (fileLoaded()) {
                            out(fileRead() + "\n")
                            end()
                        }
                        elseif (systime() > P["timeout", number]) {
                            out("Error: Timeout.\n")
                            end()
                        }
                        break
                }
                break
            
            case "write",
                if (fileCanWrite() & wait()) {
                    fileWrite(P["fs", table]["path", string] + P["path", string] + ".txt", in())
                    out("ok\n")
                    end()
                }
            
            default,
                out("Error: Unsupported action.\n")
                end()
        }
    }
    else {
        out("Error: No path in fstab\n")
        end()
    }
}
