#
# Created by scaled
#

@name cp

PEnabled["cp", number] = 1

function cp() { 
    switch (P["state", number]) {
        case 0,
            if (args() < 2) {
                out("Usage: cp [path from] [path to]\n")
                end()
            }
            else {
                P["pathFrom", string] = arg(1)
                
                if (P["pathFrom", string]:right(1) != "/") {
                    P["pathTo", string] = arg(2)
                    
                    if (P["pathTo", string]:right(1) == "/") {
                        P["pathTo", string] = P["pathTo", string] + P["pathFrom", string]:explode("/"):popString()
                    }
                    
                    local Program = exec("cat")
                    Program["args", array] = array(P["pathFrom", string])
                    P["in", array] = Program["out", array] = array()
                    P["state", number] = 1
                }
                else {
                    P["pathTo", string] = arg(2)
                    
                    if (P["pathTo", string]:right(1) == "/") {
                        local Program = exec("ls")
                        Program["args", array] = array(P["pathFrom", string])
                        P["in", array] = Program["out", array] = array()
                        
                        P["childrens", table] = table()
                        P["state", number] = 3
                    }
                    else {
                        out("Error: Can't copy directory into file.\n")
                    }
                }
            }
            break
        
        # one object
        case 1,
            if (wait()) {
                local Program = exec("touch")
                Program["args", array] = array(P["pathTo", string])
                Program["in", array] = array(in())
                
                P["in", array] = Program["out", array] = array()
                P["state", number] = 2
            }
            break
        
        case 2,
            if (wait()) {
                out(in())
                end()
            }
            break
        
        # recursive
        case 3,
            if (wait()) {
                P["list", array] = in():explode("\n")
                P["list", array]:pop()
                
                P["state", number] = 4
            }
            break
        
        case 4,
            if (P["list", array]:count() > 0) {
                local ListEntry = P["list", array]:shiftString()
                
                local Program = exec("cp")
                Program["args", array] = array(P["pathFrom", string] + ListEntry, P["pathTo", string] + ListEntry)
                Program["out", array] = P["out", array]
                
                P["childrens", table]:pushTable(Program)
            }
            else {
                P["state", number] = 5
            }
            break
        
        case 5,
            if (P["childrens", table]) {
                if (!P["childrens", table][1, table]) {
                    P["childrens", table]:remove(1)
                }
            }
            else {
                end()
            }
            break
    }
}
