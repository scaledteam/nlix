#
# Created by scaled
#

@name xmenu

if (!S:exists("xmenu")) {
    local ExitEntry = table("exit" = "exit")
    
    if (KList["poweroff", number]) { ExitEntry["poweroff", string] = "poweroff" }
    if (KList["reboot", number]) { ExitEntry["reboot", string] = "reboot" }
    if (KList["sh", number]) { ExitEntry["sh", string] = "sh" }
    
    S["etc", table]["xmenu", table] = table(
        "exit" = ExitEntry
    )
}

function xmenuAdd(Category:string, ProgramName:string, FunctionName:string) {
    if (S["etc", table]["xmenu", table]:exists(Category)) {
        S["etc", table]["xmenu", table][Category, table][ProgramName, string] = FunctionName
    }
    else {
        S["etc", table]["xmenu", table][Category, table] = table()
        S["etc", table]["xmenu", table][Category, table][ProgramName, string] = FunctionName
    }
}

function xmenuAdd(Category:string, ProgramName:string) {
    xmenuAdd(Category, ProgramName, ProgramName)
}

KList["xmenu", number] = 1

function xmenu() {
    switch (P["state", number]) {
        case 0,
            if (xcanCreate(4)) {
                P["out", array] = P["parent", table]["in", array]
                
                # Panel
                local List = S["etc", table]["xmenu", table]:keys()
                P["categories", array] = List
                
                local MenuWidth = 0
                foreach (I, Name:string = P["categories", array]) {
                    local Length = Name:length()
                    if (Length > MenuWidth) {
                        MenuWidth = Length
                    }
                }
                local MenuSize = vec2(P["xparam", table]["symbolWidth", number] * MenuWidth, List:count()) * 20
                
                P["pos", vector2] = vec2(0, P["xframe", table]["size", vector2][2] - 20 - MenuSize[2])
                
                P["menu", table] = xcreate("box"):xsize(MenuSize):xcolor(vec(63)):xpos(P["pos", vector2])
                P["menuText", table] = xcreate("textLayout"):xsize(MenuSize):xtext(List:concat("\n")):xsize(20):xpos(P["pos", vector2])
                
                P["submenu", table] = xcreate("box"):xalpha(0):xcolor(vec(63))
                P["submenuText", table] = xcreate("textLayout"):xalpha(0):xsize(20)
                
                P["xframe", table]["parent", table]["xmenu", table] = P
                
                P["state", number] = 1
            }
            break
        
        case 1,
            if (xcanDraw()) {
                local Cursor = xcursor()
                
                local PosN = int((Cursor[2] - P["pos", vector2][2]) / 20) + 1
                
                if (inrange(Cursor - P["pos", vector2], vec2(), P["menu", table]["size", vector2]) & PosN != P["posNOld", number]) {
                    P["posNOld", number] = PosN
                    
                    P["programTable", table] = S["etc", table]["xmenu", table][P["categories", array][PosN, string], table]
                    local List = P["programTable", table]:keys()
                    P["programList", array] = List
                    
                    local MenuWidth = 0
                    foreach (I, Name:string = List) {
                        local Length = Name:length()
                        if (Length > MenuWidth) {
                            MenuWidth = Length
                        }
                    }
                    
                    local MenuSize = vec2(P["xparam", table]["symbolWidth", number] * MenuWidth, List:count()) * 20
                    
                    local Pos = vec2(P["menu", table]["size", vector2][1], -MenuSize[2] + PosN * 20) + P["pos", vector2]
                    P["submenu", table]:xsize(MenuSize):xalpha(255):xpos(Pos)
                    P["submenuText", table]:xsize(MenuSize):xtext(List:concat("\n")):xalpha(255):xpos(Pos)
                }
                
                if (xkeyUse()) {
                    if (inrange(Cursor - P["pos", vector2], vec2(), P["menu", table]["size", vector2] + vec2(0, 20))) {
                    }
                    elseif (inrange(Cursor - P["submenu", table]["pos", vector2], vec2(), P["submenu", table]["size", vector2])) {
                        local PosN = int((Cursor[2] - P["submenu", table]["pos", vector2][2]) / 20) + 1
                        
                        out(P["programTable", table][P["programList", array][PosN, string], string])
                        end()
                    }
                    else {
                        end()
                    }
                }
            }
            break
    }
}
