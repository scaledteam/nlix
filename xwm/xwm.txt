#
# Created by scaled
#

@name xwm

function xwmLaunch(ProgramName:string) {
    P["xwmProgramName", string] = ProgramName
    P["parent", table]["in", array]:pushString(ProgramName)
}

function number xwmWait() {
    return P["parent", table]["programs", table][1, table]["command", string] == P["xwmProgramName", string]
}

function table xwmGetProgram() {
    return P["parent", table]["programs", table][1, table]
}

xmenuAdd("xwm", "xwm")

PExist["xwm", number] = 1

function xwm() {
    switch (P["state", number]) {
        case 0,
            if (P["parent", table] & !P["parent", table]:exists("xframe")) {
                P:removeTable("parent"):clear()
            }
            
            if (!P:exists("xframe")) {
                if (P:exists("args")) {
                    P["args", array]:unshiftString("xwm")
                }
                else {
                    P["args", array] = array("xwm")
                }
                P:changeProgram("xstart")
                break
            }
            else {
                if (!P:exists("kbd")) {
                    local Kbd = exec("kbd")
                    tackUp()
                    
                    P["kbd", array] = Kbd["out", array] = array()
                }
                
                P["in", array] = array("interface xpanel")
                
                P["programs", table] = table()
                P["frames", table] = table()
                
                P["xmsg", array] = array()
                
                P["state", number] = 1
            }
        
        case 1,
            if (xcanCreate(3)) {
                local Res = P["xframe", table]["size", vector2]
                
                local Background = xcreate("box"):xsize(Res):xcolor(P["xframe", table]:exists("parent") ? hsv2rgb(randint(360), 1, 0.7) : hsv2rgb(entity():id() * 20 % 240, 1, 0.7))
                
                local Multiplier = min(Res[1], Res[2]) / 2 * 0.3
                
                local Poses = array(
                    Multiplier * vec2(-1, 1),
                    Multiplier * vec2(-1),
                    Multiplier * vec2(-1, -0.9),
                    Multiplier * vec2(1, 0.25),
                    Multiplier * vec2(1),
                    Multiplier * vec2(1, -1)
                )
                
                xcreate("lineStrip"):xparent(Background):xvertices(Poses):xsize(Multiplier * 0.3):xcolor(vec())
                xcreate("lineStrip"):xparent(Background):xvertices(Poses):xsize(Multiplier * 0.2):xcolor(vec(191))
                
                local Count = 2.1
                P["windowSize", vector2] = floor((Res - vec2(0, 20 + 20 * Count)) / Count)
                
                P["state", number] = 2
            }
            break
            
        case 2,
            if (xcanDraw()) {
                if (P["programs", table]) {
                    # Send cursor to programs
                    local Cursor = xcursor()
                    local Use = max(xkeyUse(), 0)
                    P["xframe", table]["keyUse", number] = -1
                    P["xframe", table]["interfaceUse", number] = Use
                    
                    # Program
                    local Program = P["programs", table][1, table]
                    
                    # Check if program is dead
                    if (!Program) {
                        P["programs", table]:remove(1)
                        P["frames", table]:removeTable(1):xremove()
                    }
                    else {
                        local Frame = P["frames", table][1, table]
                        
                        # Moving windows
                        if (Frame["window", table] & Use) {
                            if (inrange(Cursor - Frame["pos", vector2], vec2(0, -20), Frame["size", vector2]:setY(0))) {
                                if (Cursor[1] > Frame["pos", vector2][1] + Frame["size", vector2][1] - 20) {
                                    Program:clear()
                                    Frame:xremove()
                                    
                                    P["programs", table]:remove(1)
                                    P["frames", table]:remove(1)
                                }
                                else {
                                    Frame["moveOffset", vector2] = Frame["pos", vector2] - Cursor
                                    
                                    Frame["keyUse", number] = 0
                                    Program["kbd", array]:clear()
                                    
                                    P["state", number] = 4
                                }
                                break
                            }
                            
                            # Action for change focus
                            if (!inrange(Cursor - Frame["pos", vector2], vec2(0, -20), Frame["size", vector2])) {
                                Frame["keyUse", number] = 0
                                Program["kbd", array]:clear()
                                
                                P["focusCounter", number] = 2
                                P["state", number] = 3
                                break
                            }
                        }
                        
                        # Keyboard
                        local KbdReal = P["kbd", array]
                        local KbdInsideProgram = Program["kbd", array]
                        
                        KbdInsideProgram:clear()
                        for (I = 1, KbdReal:count()) {
                            KbdInsideProgram[I, number] = KbdReal[I, number]
                        }
                        
                        # Send key use
                        Frame["keyUse", number] = Use
                    }
                    
                    # Infinity cycle
                    P["counter", number] = P["counter", number] % P["programs", table]:count() + 1
                    Program = P["programs", table][P["counter", number], table]
                    
                    # Check if program is dead
                    if (!Program) {
                        P["programs", table]:remove(P["counter", number])
                        P["frames", table]:removeTable(P["counter", number]):xremove()
                    }
                    # check frame size
                    elseif (Program["xframe", table]["size", vector2][1] != Program["xframe", table]["window", table]["size", vector2][1]) {
                        local Width = Program["xframe", table]["size", vector2][1]
                        Program["xframe", table]["window", table]:xsize(vec2(Width, 20))
                        
                        local Offset = vec2(Width - 10, -10)
                        Program["xframe", table]["closeButton", table]:xvertices(array(
                            vec2(-6) + Offset,
                            vec2(6) + Offset,
                            vec2() + Offset,
                            vec2(-6, 6) + Offset,
                            vec2(6, -6) + Offset
                        ))
                    }
                }
                
                # Messages
                if (P["xmsg", array]:count() > 0) {
                    P["in", array]:pushString("xmsg " + P["xmsg", array]:shiftString())
                }
                
                # Commands
                if (wait()) {
                    P["lastCommand", string] = in()
                    local CommandExp = P["lastCommand", string]:trim():explodeRE(" +")
                    
                    switch (CommandExp[1, string]) {
                        case "exit",
                            if (!P["xframe", table]:exists("parent")) {
                                P["xdevice", wirelink]:egpClear()
                            }
                            end()
                            break
                            
                        case "sh",
                            if (!P["xframe", table]:exists("parent")) {
                                P["xdevice", wirelink]:egpClear()
                                end()
                                exec("sh")
                            }
                            else {
                                end()
                            }
                            break
                            
                        case "poweroff",
                            if (!P["xframe", table]:exists("parent")) {
                                P["xdevice", wirelink]:egpClear()
                            }
                            exec("poweroff"):execNow()
                            break
                        
                        default,
                            P["commandExp", array] = CommandExp
                            
                            P["state", number] = 5
                            break
                    }
                }
            }
            break
        
        # change focus
        case 3,
            local Cursor = P["xframe", table]["cursor", vector2]
            
            local Counter = P["focusCounter", number]
            
            #for (I = 1, Overload) {
            while (perf()) {
                if (Counter <= P["programs", table]:count()) {
                    local Frame = P["frames", table][Counter, table]
                    
                    if (Frame["window", table] & inrange(Cursor - Frame["pos", vector2], vec2(0, -20), Frame["size", vector2])) {
                        if (inrange(Cursor - Frame["pos", vector2], vec2(0, -20), Frame["size", vector2]:setY(0))) {
                            if (Cursor[1] > Frame["pos", vector2][1] + Frame["size", vector2][1] - 20) {
                                P["programs", table]:removeTable(Counter):clear()
                                P["frames", table]:removeTable(Counter):xremove()
                                
                                P["state", number] = 2
                            }
                            else {
                                Frame["moveOffset", vector2] = Frame["pos", vector2] - Cursor
                                
                                P["frames", table]:unshiftTable(P["frames", table]:removeTable(Counter))
                                P["programs", table]:unshiftTable(P["programs", table]:removeTable(Counter))
                                
                                Frame:xup()
                                
                                P["state", number] = 4
                            }
                        }
                        else {
                            P["frames", table]:unshiftTable(P["frames", table]:removeTable(Counter))
                            P["programs", table]:unshiftTable(P["programs", table]:removeTable(Counter))
                            
                            Frame:xup()
                            
                            P["state", number] = 2
                        }
                        return
                    }
                    else {
                        Counter++
                    }
                }
                else {
                    P["state", number] = 2
                    break
                }
            }
            
            P["focusCounter", number] = Counter
            break
        
        case 4,
            local Cursor = xcursor()
            
            if (xkeyUse()) {
                local Frame = P["frames", table][1, table]
                Frame:xpos(round(clamp(Cursor + Frame["moveOffset", vector2], vec2(0, 20), P["xframe", table]["size", vector2] - vec2(0, 20) - Frame["size", vector2])))
            }
            else {
                P["state", number] = 2
            }
            
            P["xframe", table]["keyUse", number] = -1
            break
        
        case 5,
            if (xcanCreate(1)) {
                P["tempProgramName", string] = P["commandExp", array]:shiftString()
                
                P["tempFrame", table] = xcreate("frame")
                P["tempFrame", table]["size", vector2] = xres()
                
                if (P["tempProgramName", string] == "interface") {
                    P["tempProgramName", string] = P["commandExp", array]:shiftString()
                    P["state", number] = 7
                    break
                }
                else {
                    P["state", number] = 6
                }
            }
            else {
                break
            }
        
        case 6,
            if (xcanCreate(3)) {
                local Frame = P["tempFrame", table]
                
                local Res = xres()
                local Size = P["windowSize", vector2]
                local Pos = randvec2(vec2(0, 20), Res - vec2(0, 20) - Size)
                
                if (P["tempProgramName", string] == "big") {
                    P["tempProgramName", string] = P["commandExp", array]:shiftString()
                    Pos = vec2(0, 20)
                    Size = Res - vec2(0, 40)
                }
                
                Frame["size", vector2] = Size
                Frame:xpos(Pos)
                
                local WindowPos = vec2(0, -20)
                local WindowSize = vec2(Size[1], 20)
                
                Frame["window", table] = xcreate("box"):xparent(Frame):xpos(WindowPos):xsize(WindowSize)
                
                xcreate("text"):xparent(Frame):xpos(WindowPos):xtext(P["tempProgramName", string]):xsize(20):xcolor(vec())
                
                local Offset = vec2(WindowSize[1] - 10, -10)
                Frame["closeButton", table] = xcreate("lineStrip"):xparent(Frame):xsize(2):xvertices(array(
                    vec2(-6) + Offset,
                    vec2(6) + Offset,
                    vec2() + Offset,
                    vec2(-6, 6) + Offset,
                    vec2(6, -6) + Offset
                )):xcolor(vec())
                
                P["state", number] = 7
            }
            else {
                break
            }
        
        case 7,
            if (xcanDraw()) {
                local Frame = P:removeTable("tempFrame")
                
                local Program = Frame:xexec(P["tempProgramName", string])
                P["programs", table]:unshiftTable(Program)
                P["frames", table]:unshiftTable(Frame)
                
                if (Program != NULL) {
                    Program["args", array] = P:removeArray("commandExp")
                    Program["out", array] = P["xmsg", array]
                    Program["kbd", array] = array()
                    
                    Program["command", string] = P["lastCommand", string]
                }
                else {
                    P["xmsg", array]:pushString("Error: Program \"" + P["tempProgramName", string] + "\" is not exist.")
                }
                
                P["focus", number] = P["programs", table]:count()
                
                P["state", number] = 2
            }
            break
    }
}

