#
# Created by scaled and CornerPin
#

@name xobj

xmenuAdd("3d", "xobj")

PExist["xobj", number] = 1

function xobj() {
    switch (P["state", number]) {
        case 0,
            P["limit", number] = 75
            P["matrix", matrix] = matrix(ang(20))
            
            if (args() >= 1) {
                fread(arg(1))
                P["state", number] = 3
            }
            else {
                xwmLaunch("xfilechooser")
                P["state", number] = 1
            }
            break
        
        case 1,
            if (xwmWait()) {
                P["xfilechooser", table] = xwmGetProgram()
                P["xfilechooser", table]["out", array] = P["in", array] = array()
                
                P["state", number] = 2
            }
            break
        
        case 2,
            if (wait()) {
                fread(in())
                P["state", number] = 3
            }
            break
        
        case 3,
            if (fwait()) {
                P["data", array] = fin():explode("\n")
                
                P["model", table] = table(
                    "v" = array(),  #Vertices
                    "vt" = array(), #Texture coordinates
                    "vn" = array(), #Vertex normals
                    "vp" = array(), #Parameter space vertices
                    "f" = table()   #Faces
                )
                P["state", number] = 4
            }
            else {
                break
            }
        
        case 4,
            for (I = 1, Overload) {
                if (!perf()) {
                    break
                }
                
                if (P["data", array]:count() > 0) {
                    local Stream = P["data", array]:shiftString():trimRight():explodeRE("%s+")
                    switch (Stream[1, string]) {
                        case "v",
                            local X = Stream[2, string]:toNumber()
                            local Y = Stream[3, string]:toNumber()
                            local Z = Stream[4, string]:toNumber()
                            local W = (Stream[5, string] ?: "1"):toNumber()
                            
                            P["model", table]["v", array]:pushVector(vec(X, Y, Z) * W)
                            
                            P["scale", number] = max(P["scale", number], X, Y, Z)
                            break
                        
                        case "vt",
                            local U = Stream[2, string]:toNumber()
                            local V = Stream[3, string]:toNumber()
                            local W = (Stream[4, string] ?: "0"):toNumber()
                            
                            P["model", table]["vt", array]:pushVector(vec(U, V, W))
                            break
                        
                        case "vn",
                            local X = Stream[2, string]:toNumber()
                            local Y = Stream[3, string]:toNumber()
                            local Z = Stream[4, string]:toNumber()
                            
                            P["model", table]["vn", array]:pushVector(vec(X, Y, Z))
                            break
                        
                        case "vp",
                            local U = Stream[2, string]:toNumber()
                            local V = (Stream[3, string] ?: "0"):toNumber()
                            local W = (Stream[4, string] ?: "0"):toNumber()
                            
                            P["model", table]["vp", array]:pushVector(vec(U, V, W))
                            break
                        
                        case "f",
                            Stream:shift()
                            local ToPush = table("v" = array())
                            
                            foreach(I, Indices:string = Stream){
                                local Info = Indices:explode("/")
                                
                                #Vertex indices
                                ToPush["v", array]:pushNumber(Info[1, string]:toNumber())
                                
                                #Texture coordinate indices
                                if(Info[2, string]){
                                    if(!ToPush:exists("vt")){ToPush["vt", array] = array()}
                                    ToPush["vt", array]:pushNumber(Info[2, string]:toNumber())
                                }
                                
                                #Normal indices
                                if(Info[3, string]){
                                    if(!ToPush:exists("vn")){ToPush["vn", array] = array()}
                                    ToPush["vn", array]:pushNumber(Info[3, string]:toNumber())
                                }
                            }
                            
                            P["model", table]["f", table]:pushTable(ToPush)
                            break
                        
                        default,
                    }
                }
                else {
                    P["limit", number] = min(P["model", table]["f", table]:count(), P["limit", number])
                    
                    P["state", number] = 5
                    break
                }
            }
            break
        
        case 5,
            if (xcanCreate(P["limit", number] + 1)) {
                local Res = xres()
                
                xcreate("box"):xsize(Res):xcolor(vec())
                
                P["scale", number] = min(Res[1], Res[2]) / P["scale", number] / 3
                
                P["state", number] = 6
            }
            break
        
        case 6,
            local Scale = P["scale", number]
            
            local Res = xres()
            local Matrix = P["matrix", matrix]
            local Offset = Res / 2
            
            local V = P["model", table]["v", array]
            
            local Counter = P["counter", number]
            
            for (I = 1, Overload) {
                Counter++
                
                if (Counter > P["limit", number]) {
                    P["state", number] = 7
                    break
                }
                else {
                    local F = P["model", table]["f", table][Counter, table]["v", array]
                    
                    xcreate("polyOutline"):xvertices(array(
                        clamp(Scale * vec2(Matrix * V[F[1, number], vector]) + Offset, vec2(), Res),
                        clamp(Scale * vec2(Matrix * V[F[2, number], vector]) + Offset, vec2(), Res),
                        clamp(Scale * vec2(Matrix * V[F[3, number], vector]) + Offset, vec2(), Res),
                        clamp(Scale * vec2(Matrix * V[F[4, number], vector]) + Offset, vec2(), Res)
                    ))
                }
            }
            
            P["counter", number] = Counter
            break
        
        case 7,
            break
    }
}
