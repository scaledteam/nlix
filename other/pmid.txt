#
# Created by scaled
#

@name pmid

KList["pmid", number] = 1

#ifdef soundPlay(number, number, string)
function pmid() {
    switch (P["state", number]) {
        case 0,
            if (args() < 1) {
                out("Usage: pmid [filename]\n")
                end()
            }
            elseif (arg(1) == "stop") {
                soundPurge()
                end()
            }
            else {
                fread(fabsPath(arg(1), P["parent", table]["pwd", string]))
                
                P["state", number] = 1
            }
            break
        
        case 1,
            if (fwait()) {
                local Data = fin()
                
                if (Data:left(4) == "MThd") {
                    Data = Data:sub(9)
                    
                    # ind header
                    ## midi format
                    #Data[1] Data[2]
                    
                    ## blocks count
                    P["blocksCount", number] = (toByte(Data[3]) << 8) + toByte(Data[4])
                    out("blocks count: " + P["blocksCount", number] + "\n")
                    
                    ## The Pulses Per Quarter Note
                    P["interval", number] = 0.5 / ((toByte(Data[5]) << 8) + toByte(Data[6]))
                    
                    Data = Data:sub(7)
                    
                    P["data", string] = Data
                    
                    out("Write \"stop\" for stop playing.\n")
                    P["state", number] = 2
                }
                else {
                    out("Error: File is not midi.\n")
                    end()
                }
            }
            break
        
        case 2,
            if (P["blocksCount", number] > 0) {
                local Data = P["data", string]
                
                # block
                if (Data:left(4) == "MTrk") {
                    Data = Data:sub(5)
                    
                    P["blockLength", number] = 
                        +(toByte(Data[1]) << 24)
                        +(toByte(Data[2]) << 16)
                        +(toByte(Data[3]) << 8)
                        +(toByte(Data[4]))
                    Data = Data:sub(5)
                    
                    out("block length: " + P["blockLength", number] + "\n")
                    
                    P["data", string] = Data
                    P["blocksCount", number] = P["blocksCount", number] - 1
                    P["state", number] = 3
                }
                else {
                    out("Error: Wrong block header.\n")
                    soundPurge()
                    end()
                }
            }
            else {
                soundPurge()
                end()
            }
            break
        
        case 3,
            if (wait()) {
                if (in() == "stop") {
                    soundPurge()
                    end()
                    break
                }
            }
            
            local Data = P["data", string]
            for (J = 1, Overload) {
                if (P["state", number] != 3) {
                    break
                }
                
                switch (P["substate", number]) {
                    case 0,
                        local Time = 0
                        
                        local SuccesTime = 0
                        for (I = 1, 4) {
                            local Input = toByte(Data[I])
                            
                            Time = (Time << 7) + (Input && 0x7F)
                            
                            if ((Input >> 7) == 0) {
                                Data = Data:sub(1 + I)
                                SuccesTime = 1
                                break
                            }
                        }
                        
                        if (!SuccesTime) {
                            out("Error: Time fail.\n")
                        }
                        
                        if (Time < 500) {
                            P["nextStep", number] = systime() + Time * P["interval", number]
                        }
                        
                        P["substate", number] = 1
                            
                    case 1,
                        if (systime() > P["nextStep", number]) {
                            local Command = toByte(Data[1])
                            Data = Data:sub(2)
                            
                            local Event = Command >> 4
                            local Channel = Command && 0xF
                            
                            local Skip = 0
                            
                            switch (Event) {
                                # 1-level event 
                                ## Up note
                                case 0x8,
                                    local Note = toByte(Data[1])
                                    local Velocity = toByte(Data[2])
                                    
                                    soundStop((Channel << 8) + (Note << 16))
                                    
                                    Data = Data:sub(3)
                                    break
                                
                                ## Down note
                                case 0x9,
                                    local Note = toByte(Data[1])
                                    local Velocity = toByte(Data[2])
                                    
                                    local Id = (Channel << 8) + (Note << 16)
                                    soundPlay(Id, 100, "synth/square_1760.wav")
                                    #soundPlay(Id, 100, "synth/sine_1760.wav")
                                    #soundPlay(Id, 100, "synth/triangle_1760.wav")
                                    
                                    soundPitch(Id, (8.175 * 2 ^ (Note / 12)) * 100 / 1760)
                                    soundVolume(Id, Velocity / 128)
                                    
                                    Data = Data:sub(3)
                                    break
                                
                                # Change
                                case 0xA,
                                    local Note = toByte(Data[1])
                                    local Velocity = toByte(Data[2])
                                    Data = Data:sub(3)
                                    # Ignore
                                    break
                                
                                # 2-level event
                                case 0xB,
                                    Data = Data:sub(3)
                                    # Ignore
                                    break
                                
                                # Change program
                                case 0xC,
                                    Data = Data:sub(2)
                                    # Ignore
                                    break
                                
                                # Change canal force
                                case 0xD,
                                    local Force = toByte(Data[1])
                                    out("Force: " + Force + "\n")
                                    #soundVolume((Channel << 8) + (Note << 16), Force)
                                    
                                    Data = Data:sub(2)
                                    # Ignore
                                    break
                                
                                # Rotating of some wheel
                                case 0xE,
                                    Data = Data:sub(3)
                                    # Ignore
                                    break
                                
                                default,
                                    # Meta
                                    if (Command == 0xFF) {
                                        local Mode = toByte(Data[1])
                                        
                                        switch (Mode) {
                                            case 0x00,
                                                break
                                            
                                            case 0x01, case 0x02, case 0x03, case 0x04, case 0x05, case 0x06, case 0x07,
                                                out(Data:sub(3, 2 + toByte(Data[2])) + "\n")
                                                break
                                            
                                            case 0x20, case 0x21,
                                                break
                                            
                                            case 0x2F,
                                                out("End of block.\n")
                                                
                                                soundPurge()
                                                P["state", number] = 2
                                                break
                                            
                                            case 0x51, case 0x54, case 0x58, case 0x59, case 0x7F,
                                                break
                                            
                                            default,
                                                out("Unknown mode " + Mode:toString(16) + "\n")
                                        }
                                        Data = Data:sub(3 + toByte(Data[2]))
                                    }
                                    
                                    # SysEx
                                    elseif (Command == 0xF0 | Command == 0xF7) {
                                        out("SysEx\n")
                                        Data = Data:sub(2 + toByte(Data[1]))
                                    }
                                    
                                    # unknown
                                    else {
                                        out("Unknown command " + Command:toString(16) + "\n")
                                        Skip = 1
                                    }
                            }
                            
                            if (!Skip) {
                                P["substate", number] = 0
                            }
                        }
                        break
                }
            }
            P["data", string] = Data
            break
    }
}
#endif
