#
# Created by scaled
#

@name hotplugd

@inputs [Port1 Port2 Port3 Port4]:wirelink
@persist PORTS_COUNT
PORTS_COUNT = 4

# hot plug
if (!L:exists("hotplugd")) {
    L["hotplugd", table] = table()
}

P = L["hotplugd", table]

switch (P["state", number]) {
    case 0,
        if (!S:exists("devices")) {
            S["devices", table] = table()
            S["devices", table]["byId", table] = table()
        }
        P["state", number] = 1
        break
    
    case 1,
        local Counter = P["counter", number] + 1
        
        if (Counter > PORTS_COUNT) {
            Counter = 1
        }
        
        local Port = select(Counter,
            Port1,
            Port2,
            Port3,
            Port4
        )
        
        if (Port != P["portOld" + Counter, wirelink]) {
            if (Port != nowirelink()) {
                # Check if device exists
                local Type = Port:entity():type()
                
                local Devices = S["devices", table]
                if (!Devices:exists(Type)) {
                    Devices[Type, array] = array()
                    
                    Devices[Type, array]:pushWirelink(Port)
                    Devices["byId", table][Port:entity():id(), wirelink] = Port
                }
                elseif (Devices["byId", table][Port:entity():id(), wirelink] != Port) {
                    Devices[Type, array]:pushWirelink(Port)
                    Devices["byId", table][Port:entity():id(), wirelink] = Port
                }
            }
            
            P["portOld" + Counter, wirelink] = Port
        }
        
        P["counter", number] = Counter
        break
}
