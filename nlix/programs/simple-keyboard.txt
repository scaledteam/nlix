#
# This E2 made by scaled
#

@name simple-keyboard

switch (P["state", number]) {
    case 0,
        driverInit()
        P["state", number] = 1
        break
    
    case 1,
        if (terminalInit()) {
            P["state", number] = 2
        }
        break
    
    case 2,
        if (keyboardInit()) {
            P["keyboardOld", array] = array()
            
            eventInit()
            P["state", number] = 3
        }
        break
    
    # Wait event
    case 3,
        if (eventWaitString("get-input")) {
            P["input-sender", table] = eventGetTable()
            
            P["state", number] = 4
        }
        break
    
    # Track keyboard
    case 4,
        local Keys = keyboardGetKeys()
        local KeysOld = P["keyboardOld", array]
        
        local KeysCount = Keys:count()
        local KeysOldCount = KeysOld:count()
        
        if (KeysCount >= KeysOldCount) {
            for (I = 1, KeysCount) {
                local Key = Keys[I, number]
                if (Keys:exists(I)) {
                    if (Key != KeysOld[I, number]) {
                        switch (Key) {
                            # Enter
                            case 10, case 13, case 142,
                                terminalPrint("\n")
                                P["state", number] = 5
                                break
                            
                            # Backspace
                            case 127,
                                local KeyboardBuffer = P["KeyboardBuffer", string]
                                if (KeyboardBuffer != "") {
                                    P["KeyboardBuffer", string] = KeyboardBuffer:left(KeyboardBuffer:length() - 1)
                                    
                                    terminalErase(1)
                                }
                                break
                            
                            # Shift key
                            case 154, case 155,
                                break
                            
                            # Control key
                            case 158,
                                break
                            
                            # Normal key
                            default,
                                if (inrange(Key, 32, 127)) {
                                    local Char = toChar(Key)
                                    
                                    P["KeyboardBuffer", string] = P["KeyboardBuffer", string] + Char
                                    
                                    terminalPrint(Char)
                                }
                        }
                    }
                    KeysOld[I, number] = Key
                }
                else {
                    for (J = I, KeysCount) {
                        KeysOld:remove(I)
                    }
                    break
                }
            }
        }
        else {
            for (I = 1, KeysCount) {
                KeysOld[I, number] = Keys[I, number]
            }
            KeysCount++
            for (I = KeysCount, KeysOldCount) {
                KeysOld:remove(KeysCount)
            }
        }
        
        break
    
    # Send ready data
    case 5,
        P["input-sender", table]:eventSend("get-input")
        P["input-sender", table]:eventSend(P["KeyboardBuffer", string])
        
        P["KeyboardBuffer", string] = ""
        
        P["state", number] = 3
        break
}
