#
# This E2 made by scaled
#

@name simple-keyboard

switch (stateGet()) {
    case 0,
        if (terminalInit()) {
            stateNext()
        }
        break
    
    case 1,
        if (keyboardInit()) {
            varWrite("keyboardOld", array())
            
            eventInit()
            stateNext()
        }
        break
    
    # Wait event
    case 2,
        if (eventWaitString("get-input")) {
            local Sender = eventGetTable()
            varWrite("input-sender", Sender)
            
            stateNext()
        }
        break
    
    # Track keyboard
    case 3,
        local Keys = keyboardGetKeys()
        local KeysOld = varArray("keyboardOld")
        
        local KeysCount = Keys:count()
        local KeysOldCount = KeysOld:count()
        
        if (KeysCount >= KeysOldCount) {
            for (I = 1, KeysCount) {
                local Key = Keys[I, number]
                if (Keys:exists(I)) {
                    if (Key != KeysOld[I, number]) {
                        switch (Key) {
                            # Enter
                            case 10, case 13, case 142,
                                terminalPrint("\n")
                                stateNext()
                                break
                            
                            # Backspace
                            case 127,
                                local KeyboardBuffer = varString("KeyboardBuffer")
                                if (KeyboardBuffer != "") {
                                    varWrite("KeyboardBuffer", KeyboardBuffer:left(KeyboardBuffer:length() - 1))
                                    
                                    terminalErase(1)
                                }
                                break
                            
                            # Shift key
                            case 154, case 155,
                                break
                            
                            # Control key
                            case 158,
                                break
                            
                            # Normal key
                            default,
                                if (inrange(Key, 32, 127)) {
                                    local Char = toChar(Key)
                                    
                                    varWrite("KeyboardBuffer", varString("KeyboardBuffer") + Char)
                                    
                                    terminalPrint(Char)
                                }
                        }
                    }
                    KeysOld[I, number] = Key
                }
                else {
                    for (J = I, KeysCount) {
                        KeysOld:remove(I)
                    }
                    break
                }
            }
        }
        else {
            for (I = 1, KeysCount) {
                KeysOld[I, number] = Keys[I, number]
            }
            KeysCount++
            for (I = KeysCount, KeysOldCount) {
                KeysOld:remove(KeysCount)
            }
        }
        
        break
    
    # Send ready data
    case 4,
        local Sender = varTable("input-sender")
        Sender:eventSend("get-input")
        Sender:eventSend(varString("KeyboardBuffer"))
        
        varWrite("KeyboardBuffer", "")
        
        stateSet(2)
        break
}
