#
# Created by scaled
#

@name hotplugd

switch (stateGet()) {
    # Hot plug
    case 0,
        if (!ThisEnvironment:exists("devices")) {
            ThisEnvironment["devices", table] = table()
        }
        
        local Counter = varNumber("counter") + 1
        
        local Port = select(Counter,
            Port1,
            Port2,
            Port3,
            Port4
        )
        
        if ((Port != varWirelink("PortOld" + Counter)) & Port) {
            # Check if device exists
            local DeviceExists = 0
            local DeviceType = Port:entity():type()
            local Devices = ThisEnvironment["devices", table]
            
            for (I = 1, Devices:count()) {
                if (Devices[I, table]["port", wirelink]:entity():type() == DeviceType & Devices[I, table]["port", wirelink] == Port) {
                    DeviceExists = 1
                    break
                }
            }
            
            if (!DeviceExists) {
                Devices:pushTable(Port:toDevice())
            }
        }
        varWrite("PortOld" + Counter, Port)
        
        if (Counter >= PORTS_COUNT) {
            if (ThisEnvironment["devices", table]:count() > 0) {
                stateNext()
            }
            varWrite("counter", 0)
        }
        else {
            varWrite("counter", Counter)
        }
        break
    
    # Hot unplug
    case 1,
        local Counter = varNumber("counter") + 1
        local Devices = ThisEnvironment["devices", table]
        
        local Device = Devices[Counter, table]
        if (Device:exists("port") & !Device["port", wirelink]:entity():isValid()) {
            Device:clear()
            Devices:remove(Counter)
            Counter--
        }
        
        if (Counter >= Devices:count()) {
            statePrev()
            varWrite("counter", 0)
        }
        else {
            varWrite("counter", Counter)
        }
        break
}
