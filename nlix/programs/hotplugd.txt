#
# Created by scaled
#

@name hotplugd

switch (P["state", number]) {
    # Hot plug
    case 0,
        if (!E:exists("devices")) {
            E["devices", table] = table()
        }
        
        local Counter = P["counter", number] + 1
        
        local Port = select(Counter,
            Port1,
            Port2,
            Port3,
            Port4
        )
        
        if ((Port != P["PortOld" + Counter, wirelink]) & Port) {
            # Check if device exists
            local DeviceExists = 0
            local DeviceType = Port:entity():type()
            local Devices = E["devices", table]
            
            for (I = 1, Devices:count()) {
                if (Devices[I, table]["port", wirelink]:entity():type() == DeviceType & Devices[I, table]["port", wirelink] == Port) {
                    DeviceExists = 1
                    break
                }
            }
            
            if (!DeviceExists) {
                Devices:pushTable(Port:toDevice())
            }
        }
        P["PortOld" + Counter, wirelink] = Port
        
        if (Counter >= PORTS_COUNT) {
            if (E["devices", table]:count() > 0) {
                P["state", number] = 1
            }
            P["counter", number] = 0
        }
        else {
            P["counter", number] = Counter
        }
        break
    
    # Hot unplug
    case 1,
        local Counter = P["counter", number] + 1
        local Devices = E["devices", table]
        
        local Device = Devices[Counter, table]
        if (Device:exists("port") & !Device["port", wirelink]:entity():isValid()) {
            Device:clear()
            Devices:remove(Counter)
            Counter--
        }
        
        if (Counter >= Devices:count()) {
            P["state", number] = 0
            P["counter", number] = 0
        }
        else {
            P["counter", number] = Counter
        }
        break
}

