#
# Created by scaled
#

@name driver-gl

switch (P["state", number]) {
    # Initialize
    case 0,
        eventInit()
        driverInit()
        P["state", number] = 1
        break
    
    case 1,
        if (driverPopDevice("gmod_wire_egp")) {
            local Port = P["device", table]["port", wirelink]
            Port:egpClear()
            Port:egpDrawTopLeft(1)
            Port:egpResolution(vec2(-256), vec2(256))
            
            Port:egpBox(1, vec2(-256), vec2(512))
            Port:egpColor(1, vec())
            
            local Id = min(egpMaxObjects(), 300)
            while (Id >= 2) {
                Port:egpLine(Id, vec2(), vec2())
                Id--
            }
            
            P["state", number] = 2
        }
        break
    
    case 2,
        P["usedIds", array] = array()
        
        P["idle", number] = 1
        P["state", number] = 3
        break
    
    # Wait events
    case 3,
        local Device = P["device", table]
        local Port = Device["port", wirelink]
        if (Device:isValid()) {
            for (I = 1, KERNEL_OVERLOAD) {
                if (eventExists()) {
                    P["idle", number] = 0
                    local Command = eventGetString()
                    
                    switch (Command) {
                        case "glStart",
                            local Id = min(egpMaxObjects(), 300)
                            
                            local UsedIds = P["usedIds", array]
                            local UsedIdsOld = P["usedIdsOld", array]
                            while (Id >= 2) {
                                if (!UsedIds:exists(Id) & UsedIdsOld:exists(Id)) {
                                    Port:egpLine(Id, vec2(), vec2())
                                    UsedIds:remove(Id)
                                }
                                Id--
                            }
                            P["usedIdsOld", array] = UsedIds
                            
                            UsedIdsOld:clear()
                            P["usedIds", array] = UsedIdsOld
                            break
                        
                        case "glFillColor",
                            Port:egpColor(1, eventGetVector())
                            break
                        
                        case "glColor",
                            P["color", vector] = eventGetVector()
                            break
                        
                        case "glPos",
                            P["pos", vector] = eventGetVector()
                            break
                        
                        case "glAng",
                            P["matrix", matrix] = matrix(eventGetAngle())
                            break
                        
                        case "glTriangle",
                            local Matrix = P["matrix", matrix]
                            local Pos = P["pos", vector]
                            
                            local Pos1 = Matrix * (eventGetVector() - Pos)
                            local Pos2 = Matrix * (eventGetVector() - Pos)
                            local Pos3 = Matrix * (eventGetVector() - Pos)
                            
                            local UsedIds = P["usedIds", array]
                            local Id = int(min(egpMaxObjects(), 300) / 10 * (Pos1 + Pos2 + Pos3)[3] / 3) + 1
                            
                            while (UsedIds:exists(Id)) {
                                Id++
                            }
                            UsedIds[Id, number] = 1
                            
                            if (Pos1[3] > 0 & Pos2[3] > 0 & Pos3[3] > 0) {
                                Port:egpTriangle(Id,
                                    256 * Pos1:dehomogenized(),
                                    256 * Pos2:dehomogenized(),
                                    256 * Pos3:dehomogenized()
                                )
                                Port:egpColor(Id, P["color", vector])
                            }
                            break
                        
                        case "exit",
                            Port:egpClear()
                            driverPushDevice()
                            programKill()
                            break
                        
                        default,
                    }
                }
                else {
                    P["idle", number] = 1
                    break
                }
            }
        }
        else {
            driverPushDevice()
            P["state", number] = 1
        }
        break
    }
