#
# Created by scaled
#

@name driver-terminal

switch (P["state", number]) {
    # Initialize
    case 0,
        eventInit()
        driverInit()
        P["state", number] = 1
        break
    
    case 1,
        if (driverPopDevice("gmod_wire_textscreen")) {
            local Port = P["device", table]["port", wirelink]
            Port["String", string] = P["consoleText", string]
            
            P["resolution", vector2] = vec2(39, 21)
            
            P["state", number] = 2
        }
        
        elseif (driverPopDevice("gmod_wire_consolescreen")) {
            local Port = P["device", table]["port", wirelink]
            Port[2041] = 1
            Port:writeString(P["consoleText", string], 0, 0)
            
            P["resolution", vector2] = vec2(30, 18)
            P["state", number] = 2
        }
        
        elseif (driverPopDevice("gmod_wire_egp")) {
            local Port = P["device", table]["port", wirelink]
            Port:egpClear()
            Port:egpDrawTopLeft(1)
            
            Port:egpTextLayout(1, P["consoleText", string], vec2(), vec2(512))
            Port:egpFont(1, FONT_MONOSPACED)
            
            P["resolution", vector2] = vec2(56, 28)
            
            P["state", number] = 2
        }
        
        elseif (driverPopDevice("virtual-terminal")) {
            local Device = P["device", table]
            Device["new-output", string] = P["consoleText", string]
            
            P["resolution", vector2] = Device["resolution", vector2]
            
            P["state", number] = 2
        }
        
        elseif (driverPopDevice("virtual-egp-screen")) {
            local Device = P["device", table]
            Device["output-queue", array] = Device["output-queue", array]:add(array(
                "egpBox", 1, vec2(), Device["resolution", vector2],
                "egpParentToBase", 1,
                "egpColor", 1, vec(),
                "egpTextLayout", 2, P["consoleText", string], vec2(), Device["resolution", vector2],
                "egpParentToBase", 2
            ))
            
            P["resolution", vector2] = Device["resolution", vector2] / vec2(512) * vec2(56, 28)
            
            P["state", number] = 2
        }
        
        break
    
    # Wait events
    case 2,
        local Device = P["device", table]
        if (Device:isValid()) {
            if (eventExists()) {
                local ScreenType = Device:type()
                
                local Command = eventGetString()
                switch (Command) {
                    case "clear",
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, "")
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ""
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = P["consoleText", string]:length()
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString("")
                                break
                        }
                        
                        P["consoleText", string] = ""
                        P["ConsoleTextArray", array]:clear()
                        break
                    
                    case "print",
                        local NewPrint = eventGetString()
                        
                        local Width = P["resolution", vector2][1]
                        local Height = P["resolution", vector2][2]
                        
                        if (!P:exists("ConsoleTextArray")) {
                            P["ConsoleTextArray", array] = array()
                        }
                        
                        local ConsoleTextArray = P["ConsoleTextArray", array]
                        local LastString = ConsoleTextArray:count()
                        
                        local NewText = ConsoleTextArray:popString() + NewPrint
                        local LineOffset = -1
                        local NewTextLength = NewText:length()
                        local NewTextExplode = NewText:explode("\n")
                        
                        foreach (I, NewText2:string = NewTextExplode) {
                            for (I = 0, NewText2:length() / Width) {
                                ConsoleTextArray:pushString(NewText2:sub(I * Width, (I + 1) * Width))
                                LineOffset++
                            }
                        }
                        
                        while (ConsoleTextArray:count() > Height) {
                            ConsoleTextArray:shift()
                        }
                        
                        local ConsoleText = ConsoleTextArray:concat("\n")
                        P["consoleText", string] = ConsoleText
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, ConsoleText)
                                break
                            
                            case "gmod_wire_consolescreen",
                                if (LineOffset != 0) {
                                    Device["port", wirelink][2041] = 1
                                }
                                Device["port", wirelink]:writeString(ConsoleText, 0, 0)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ConsoleText
                                break
                            
                            case "virtual-terminal",
                                Device["new-output", string] = NewPrint
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString(ConsoleText)
                                break
                        }
                        break
                    
                    case "erase",
                        local EraseCount = eventGetNumber()
                        
                        P["consoleText", string] = P["consoleText", string]:left(P["consoleText", string]:length() - EraseCount)
                        
                        local ConsoleTextArray = P["ConsoleTextArray", array]
                        
                        while (EraseCount > 0) {
                            local String = ConsoleTextArray:popString()
                            local StringCount = String:length()
                            
                            if (StringCount == 0) {
                                break
                            }
                            elseif (EraseCount < StringCount) {
                                ConsoleTextArray:pushString(String:left(StringCount - EraseCount))
                                break
                            }
                            else {
                                EraseCount -= StringCount
                            }
                        }
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, P["consoleText", string])
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                Device["port", wirelink]:writeString(P["consoleText", string], 0, 0)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = P["consoleText", string]
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = EraseCount
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString(P["consoleText", string])
                                break
                        }
                        break
                    
                    case "exit",
                        driverPushDevice()
                        programKill()
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpClear()
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = P["consoleText", string]:length()
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpRemove")
                                OutputQueue:pushNumber(1)
                                
                                OutputQueue:pushString("egpRemove")
                                OutputQueue:pushNumber(2)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ""
                                break
                        }
                        break
                }
            }
        }
        else {
            driverPushDevice()
            P["state", number] = 1
        }
        break
}
