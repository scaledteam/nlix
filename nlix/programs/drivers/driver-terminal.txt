#
# Created by scaled
#

@name driver-terminal

switch (stateGet()) {
    # Initialize
    case 0,
        if (driverPopDevice("gmod_wire_consolescreen")) {
            local Port = varTable("device")["port", wirelink]
            Port[2041] = 1
            Port:writeString(varString("consoleText"), 0, 0)
            
            stateNext()
        }
        
        elseif (driverPopDevice("gmod_wire_egp")) {
            local Port = varTable("device")["port", wirelink]
            Port:egpClear()
            Port:egpDrawTopLeft(1)
            
            Port:egpTextLayout(1, varString("consoleText"), vec2(), vec2(512))
            Port:egpFont(1, FONT_MONOSPACED)
            
            stateNext()
        }
        
        elseif (driverPopDevice("gmod_wire_textscreen")) {
            local Port = varTable("device")["port", wirelink]
            Port["String", string] = varString("consoleText")
            
            stateNext()
        }
        
        elseif (driverPopDevice("virtual-terminal")) {
            local Device = varTable("device")
            Device["new-output", string] = varString("consoleText")
            
            stateNext()
        }
        
        elseif (driverPopDevice("virtual-egp-screen")) {
            local Device = varTable("device")
            Device["output-queue", array] = Device["output-queue", array]:add(array(
                "egpBox", 1, vec2(), Device["resolution", vector2],
                "egpParentToBase", 1,
                "egpColor", 1, vec(),
                "egpTextLayout", 2, varString("consoleText"), vec2(), Device["resolution", vector2],
                "egpParentToBase", 2
            ))
            
            stateNext()
        }
        
        break
    
    # Wait events
    case 1,
        local Device = varTable("device")
        if (Device:isValid()) {
            if (eventsExists()) {
                local ScreenType = Device:type()
                
                local Command = eventGetString()
                switch (Command) {
                    case "clear",
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, "")
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ""
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = varString("consoleText"):length()
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString("")
                                break
                        }
                        
                        varWrite("consoleText", "")
                        break
                    
                    case "print",
                        local NewPrint = eventGetString()
                        local ConsoleText = varString("consoleText") + NewPrint
                        varWrite("consoleText", ConsoleText)
                        
                        #[local Width = 10
                        local ConsoleText = varString("consoleText")
                        local ThisLineCount = ConsoleText:length() % Width
                        local ThisLine = ConsoleText:sub(ConsoleText:length() - ThisLineCount, ConsoleText:length()) + NewPrint
                        
                        local ThisLineArray = array()
                        for (I = 1, int(ThisLine:length() / Width)) {
                            ThisLineArray[I, string] = ThisLine:sub(1, I * Width)
                        }
                        
                        ConsoleText += ConsoleText:sub(1, ConsoleText:length() - ThisLineCount) + ThisLineArray:concat("\n")
                        varWrite("consoleText", ConsoleText)]#
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, ConsoleText)
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink]:writeString(ConsoleText, 0, 0)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ConsoleText
                                break
                            
                            case "virtual-terminal",
                                Device["new-output", string] = NewPrint
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString(ConsoleText)
                                break
                        }
                        break
                    
                    case "erase",
                        local EraseCount = eventGetNumber()
                        varWrite("consoleText", varString("consoleText"):left(varString("consoleText"):length() - EraseCount))
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpSetText(1, varString("consoleText"))
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                Device["port", wirelink]:writeString(varString("consoleText"), 0, 0)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = varString("consoleText")
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = EraseCount
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpSetText")
                                OutputQueue:pushNumber(2)
                                OutputQueue:pushString(varString("consoleText"))
                                break
                        }
                        break
                    
                    case "exit",
                        driverPushDevice()
                        programKill()
                        
                        switch (ScreenType) {
                            case "gmod_wire_egp",
                                Device["port", wirelink]:egpClear()
                                break
                            
                            case "gmod_wire_consolescreen",
                                Device["port", wirelink][2041] = 1
                                break
                            
                            case "virtual-terminal",
                                Device["new-erase", number] = varString("consoleText"):length()
                                break
                            
                            case "virtual-egp-screen",
                                local OutputQueue = Device["output-queue", array]
                                
                                OutputQueue:pushString("egpRemove")
                                OutputQueue:pushNumber(1)
                                
                                OutputQueue:pushString("egpRemove")
                                OutputQueue:pushNumber(2)
                                break
                            
                            case "gmod_wire_textscreen",
                                Device["port", wirelink]["String", string] = ""
                                break
                        }
                        break
                }
            }
        }
        else {
            driverPushDevice()
            stateSet(0)
        }
        break
}
