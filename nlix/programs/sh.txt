#
# This E2 made by scaled
#

@name sh

switch (P["state", number]) {
    case 0,
        if (terminalInit()) {
            terminalPrint("Welcome to the Nlix operating system!\n")
            P["state", number] = 1
        }
        break
    
    case 1,
        if (keyboardInit()) {
            P["state", number] = 2
        }
        break
    
    case 2,
        if (simpleKeyboardInit()) {
            P["state", number] = 3
        }
        break
    
    case 3,
        if (!E:exists("startTime")) {
            E["startTime", number] = systime()
        }
        P["state", number] = 4
        break
    
    case 4,
        #terminalPrint(ThisProgram["user", string] + " $ ")
        terminalPrint("sh $ ")
        simpleKeyboardRequestInput()
        
        P["state", number] = 5
        break
    
    case 5,
        if (simpleKeyboardWaitInput()) {
            local Command = simpleKeyboardGetInput()
            if (Command == "") {
                P["state", number] = 4
            }
            else {
                local Program = NULL
                Command = Command:trim()
                
                local CommandExplode = Command:explode(" ")
                
                local I = 1
                while (I < CommandExplode:count()) {
                    if (CommandExplode[I, string] == "") {
                        CommandExplode:remove(I)
                    }
                    else {
                        I++
                    }
                }
                
                local ProgramName = CommandExplode:shiftString()
                local Program = programStart(ProgramName)
                if (CommandExplode:count() > 0) {
                    Program["args", array] = CommandExplode
                }
                
                P["LaunchedApp", table] = Program
                P["state", number] = 6
            }
        }
        
        break
    
    case 6,
        if (!P["LaunchedApp", table]:isValid()) {
            P["state", number] = 4
        }
        
        local KeyboardKeys = keyboardGetKeys()
        if (changed(KeyboardKeys:count())) {
            if (KeyboardKeys[1, number] == 158) {
                if (KeyboardKeys[2, number] == 99) {
                    P["LaunchedApp", table]:programKill()
                }
                if (KeyboardKeys[2, number] == 122) {
                    P["LaunchedApp", table]:eventSend("exit")
                }
            }
        }
        break
}
