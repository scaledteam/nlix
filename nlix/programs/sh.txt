#
# This E2 made by scaled
#

@name sh

switch (stateGet()) {
    case 0,
        if (terminalInit()) {
            terminalPrint("Welcome to the Nlix operating system!\n")
            stateNext()
        }
        break
    
    case 1,
        if (keyboardInit()) {
            stateNext()
        }
        break
    
    case 2,
        if (simpleKeyboardInit()) {
            stateNext()
        }
        break
    
    case 3,
        if (!ThisEnvironment:exists("startTime")) {
            ThisEnvironment["startTime", number] = systime()
        }
        stateNext()
        break
    
    case 4,
        #terminalPrint(ThisProgram["user", string] + " $ ")
        terminalPrint("sh $ ")
        simpleKeyboardRequestInput()
        
        stateNext()
        break
    
    case 5,
        if (simpleKeyboardWaitInput()) {
            local Command = simpleKeyboardGetInput()
            if (Command == "") {
                statePrev()
            }
            else {
                local Program = NULL
                Command = Command:trim()
                
                local CommandExplode = Command:explode(" ")
                
                local I = 1
                while (I < CommandExplode:count()) {
                    if (CommandExplode[I, string] == "") {
                        CommandExplode:remove(I)
                    }
                    else {
                        I++
                    }
                }
                
                local ProgramName = CommandExplode:shiftString()
                local Program = programStart(ProgramName)
                if (CommandExplode:count() > 0) {
                    Program:varWrite("args", CommandExplode)
                }
                
                varWrite("LaunchedApp", Program)
                stateNext()
            }
        }
        
        break
    
    case 6,
        if (!varTable("LaunchedApp"):isValid()) {
            stateShift(-2)
        }
        
        local KeyboardKeys = keyboardGetKeys()
        if (changed(KeyboardKeys:count())) {
            if (KeyboardKeys[1, number] == 158) {
                if (KeyboardKeys[2, number] == 99) {
                    varTable("LaunchedApp"):programKill()
                }
                if (KeyboardKeys[2, number] == 122) {
                    varTable("LaunchedApp"):eventSend("exit")
                }
            }
        }
        break
}
