#
# This E2 made by scaled
#

@name environmentctl

switch (P["state", number]) {
    # Wait terminal driver
    case 0,
        if (terminalInit()) {
            terminalPrint("Environment control. Write \"quit\" for exit.\n")
            P["state", number] = 1
        }
        break
    
    # Wait keyboard driver
    case 1,
        if (simpleKeyboardInit()) {
            P["state", number] = 2
        }
        break
    
    # Main state
    case 2,
        terminalPrint("> ")
        simpleKeyboardRequestInput()
        
        P["state", number] = 3
        break
    
    # Get input
    case 3,
        if (simpleKeyboardWaitInput()) {
            local Input = simpleKeyboardGetInput()
            local InputExplode = Input:explode(" ")
            local Command = InputExplode[1, string]
            switch (Command) {
                case "",
                    P["state", number] = 2
                    break
                
                case "ws-term",
                    if (devicePop("gmod_wire_textscreen") | devicePop("gmod_wire_consolescreen") | devicePop("gmod_wire_egp")) { }
                    if (devicePop("gmod_wire_keyboard") | devicePop("gmod_wire_textentry")) { }
                    
                    local Devices = P["devices", table]
                    if (Devices:count() == 2) {
                        local Environment = table()
                        Environment["devices", table] = table()
                        
                        while (Devices:count() > 0) {
                            local Device = Devices:popTable()
                            Device:remove("program")
                            Environment["devices", table]:pushTable(Device)
                        }
                        
                        local Autostart = array(
                            "driver-terminal",
                            "driver-keyboard",
                            "simple-keyboard",
                            "sh"
                        )
                        foreach (I, ProgramName:string = Autostart) {
                            Environment:programStart(ProgramName)
                        }
                    }
                    else {
                        devicePushAll()
                    }
                    
                    P["state", number] = 2
                    break
                
                case "dev-list",
                    local Message = ""
                    for (I = 1, E["devices", table]:count()) {
                        local Device = E["devices", table][I, table]
                        Message += I + ", " + (Device:deviceIsBusy() ? "busy" : "free") + ", " + Device:type() + "\n"
                    }
                    terminalPrint(Message)
                    P["state", number] = 2
                    break
                
                case "dev-del",
                    local Id = InputExplode[2, string]:toNumber()
                    E["devices", table][Id, table]:clear()
                    E["devices", table]:remove(Id)
                    P["state", number] = 2
                    break
                
                case "quit",
                    programKill()
                    break
                
                case "exit",
                    programKill()
                    break
                
                case "help",
                    local Message = "ws-term\n" +
                        "ws-egp\n" +
                        "dev-list\n" +
                        "dev-del\n" +
                        "quit\n" + 
                        "exit\n" +
                        "clear\n" + 
                        "help\n"
                    terminalPrint(Message)
                    P["state", number] = 2
                    break
                
                default,
                    local Message = "Wrong command\n"
                    terminalPrint(Message)
                    P["state", number] = 2
                    break
            }
        }
        break
}
