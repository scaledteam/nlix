#
# This E2 made by scaled
#

@name simple-keyboard

if (!APPS_INFO:exists("simple-keyboard")) {
    local Info = table()
    
    APPS_INFO["simple-keyboard", table] = Info
}

if (app("simple-keyboard")) {
    switch (ThisAppState) {
        case 0,
            if (initTerminal()) {
                appNextState()
            }
            break
        
        case 1,
            if (initKeyboard()) {
                appR("keyboardOld", array())
                
                appNextState()
            }
            break
        
        # Wait event
        case 2,
            if (waitEvent("get-input")) {
                local Sender = getEventDataN()
                appN("input-sender", Sender)
                
                appNextState()
            }
            break
        
        # Track keyboard
        case 3,
            local Keys = keyboardGetKeys()
            local KeyboardOldKeys = appR("keyboardOld")
            if (Keys[1, number] >= KeyboardOldKeys[1, number]) {
                for (I = 2, 5) {
                    if (
                        (Keys[I, number] != KeyboardOldKeys[I, number])
                        & Keys[I, number]
                    ) {
                        local Key = Keys[I, number]
                        #print(Key)
                        
                        # Enter
                        if (Key == 10 | Key == 142) {
                            consolePrint("\n")
                            appNextState()
                        }
                        # Backspace
                        elseif (Key == 127) {
                            if (appN("KeyboardBufferCount") > 0) {
                                appS("KeyboardBuffer", appS("KeyboardBuffer"):left(appN("KeyboardBufferCount") - 1))
                                appN("KeyboardBufferCount", appN("KeyboardBufferCount") - 1)
                                
                                consoleErase(1)
                            }
                        }
                        # Shift key
                        elseif (Key == 154 | Key == 155) {}
                        # Normal key
                        else {
                            local Char = toUnicodeChar(Key)
                            
                            appS("KeyboardBuffer", appS("KeyboardBuffer") + Char)
                            appN("KeyboardBufferCount", appN("KeyboardBufferCount") + 1)
                            
                            consolePrint(Char)
                        }
                    }
                }
            }
            
            for (I = 1, 5) {
                KeyboardOldKeys[I, number] = Keys[I, number]
            }
            break
        
        # Send ready data
        case 4,
            local Sender = appN("input-sender")
            sendEvent(Sender, "get-input")
            sendEventData(Sender, appS("KeyboardBuffer"))
            
            appS("KeyboardBuffer", "")
            appN("KeyboardBufferCount", 0)
            
            appSetState(2)
            break
    }
    
}
