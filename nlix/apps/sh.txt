#
# This E2 made by scaled
#

@name sh

switch (ThisAppState) {
    case 0,
        if (initTerminal()) {
            terminalPrint("Welcome to the Nlix operation system!\n")
            appNextState()
        }
        break
    
    case 1,
        if (initKeyboard()) {
            appNextState()
        }
        break
    
    case 2,
        if (initSimpleKeyboard()) {
            appNextState()
        }
        break
    
    case 3,
        terminalPrint(ThisAppUser + " $ ")
        requestKeyboardInput()
        
        appNextState()
        break
    
    case 4,
        if (waitKeyboardInput()) {
            local Command = getKeyboardInput()
            if (Command == "") {
                appSetState(3)
            }
            else {
                local App = command(Command)
                
                if (App != NULL) {
                    varWrite("LaunchedApp", App)
                    appNextState()
                }
                else {
                    terminalPrint("Error! App \"" + Command + "\" not found.\n")
                    appSetState(3)
                }
            }
        }
        
        break
    
    case 5,
        if (!varTable("LaunchedApp"):appIsLaunched()) {
            appSetState(3)
        }
        
        local KeyboardKeys = keyboardGetKeys()
        if (KeyboardKeys[2, number] == 158 & KeyboardKeys[3, number] == 99) {
            varTable("LaunchedApp"):appClose()
        }
        break
}
