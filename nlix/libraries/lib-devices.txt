#
# Created by scaled
#

@name lib-devices

@inputs [Port1 Port2 Port3 Port4]:wirelink
@persist PORTS_COUNT
PORTS_COUNT = 4

function number table:deviceIsBusy() {
    return This["program", table]:isValid()
}

function table table:deviceGet()    { return This["devices", table][1, table] }
function table table:deviceGet(Id)  { return This["devices", table][Id, table] }

function table deviceGet()          { return ThisProgram["devices", table][1, table] }
function table deviceGet(Id)        { return ThisProgram["devices", table][Id, table] }

function number devicePop(DeviceType:string) {
    for (I = 1, ThisEnvironment["devices", table]:count()) {
        local Device = ThisEnvironment["devices", table][I, table]
        if (Device:type() == DeviceType & !Device:deviceIsBusy()) {
            Device["program", table] = ThisProgram
            
            if (!ThisProgram:exists("devices")) {
                ThisProgram["devices", table] = table()
            }
            ThisProgram["devices", table]:pushTable(Device)
            return 1
        }
    }
    return 0
}

function number devicePopAll(DeviceType:string) {
    local Count = 0
    for (I = 1, ThisEnvironment["devices", table]:count()) {
        local Device = ThisEnvironment["devices", table][I, table]
        if (Device:type() == DeviceType & !Device:deviceIsBusy()) {
            Device["program", table] = ThisProgram
            ThisProgram["devices", table]:pushTable(Device)
            Count++
        }
    }
    return Count
}

function devicePush() {
    ThisProgram["devices", table][1, table]:remove("program")
    ThisProgram["devices", table]:remove(1)
}

function devicePush(Id) {
    ThisProgram["devices", table][Id, table]:remove("program")
    ThisProgram["devices", table]:remove(Id)
}

function devicePushAll() {
    local ThisProgramDevices = ThisProgram["devices", table]
    for (I = 1, ThisProgramDevices:count()) {
        ThisProgramDevices[I, table]:remove("program")
    }
    ThisProgramDevices:clear()
}

function table wirelink:toDevice() {
    local Device = object(This:entity():type())
    Device["port", wirelink] = This
    
    return Device
}
