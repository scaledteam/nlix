#
# Created by scaled
#

@name xremove

P2F["xremove", string] = "xremove"

function xremove() {
    for (I = 1, Overload) {
        switch (P["state", number]) {
            case 0,
                if (!P["xparam", table]["blocked", number]) {
                    P["xparam", table]["blocked", number] = 1
                    
                    P["removedObjectsIds", table] = table()
                    
                    P["state", number] = 1
                }
                else {
                    break
                }
            
            case 1,
                local Id = P["id", number]
                
                if (Id <= P["idEnd", number]) {
                    local Object = P["xobjectsAll", table][Id, table]
                    
                    Object:xclear()
                    P["removedObjectsIds", table][Id, number] = 1
                    
                    Id++
                    
                    P["id", number] = Id
                    break
                }
                else {
                    P["id", number] = 1
                    P["state", number] = 2
                }
            
            case 2,
                P["id", number] = P["id", number] + 1
                local Id = P["id", number]
                
                if (Id <= P["xobjectsAll", table]:count()) {
                    local Object = P["xobjectsAll", table][Id, table]
                    
                    if (!P["removedObjectsIds", table]:exists(Id)) {
                        if (Object:exists("removed")) {
                            P["removedObjectsIds", table][Id, number] = 1
                        }
                        elseif (Object["parent", table]:exists("removed") & Object:exists("parent")) {
                            Object:xclear()
                            P["removedObjectsIds", table][Id, number] = 1
                            P["removed", number] = P["removed", number] + 1
                        }
                    }
                    break
                }
                else {
                    if (P["removed", number] > 0) {
                        P["removed", number] = 0
                        P["id", number] = 1
                        break
                    }
                    else {
                        P["id", number] = 1
                        P["state", number] = 3
                    }
                }
            
            case 3,
                local XobjectsAll = P["xobjectsAll", table]
                
                local Id = P["id", number]
                local Offset = P["offset", number]
                
                if (Id <= XobjectsAll:count()) {
                    if (P["removedObjectsIds", table][Id + Offset, number]) {
                        P["removedObjectsIds", table][Id + Offset, number] = 0
                        P["offset", number] = Offset + 1
                        
                        P["xdevice", wirelink]:egpRemove(XobjectsAll:count())
                        #P["xdevice", wirelink]:egpLine(XobjectsAll:count(), vec2(), vec2())
                        XobjectsAll:remove(Id)
                    }
                    else {
                        if (Offset != 0) {
                            local Object = XobjectsAll[Id, table]
                            
                            Object["id", number] = Id
                            Object:xredraw()
                        }
                        
                        P["id", number] = Id + 1
                    }
                    break
                }
                else {
                    P["removedObjectsIds", table]:clear()
                    P["xparam", table]["blocked", number] = 0
                    end()
                    return
                }
                break
        }
    }
}
