#
# Created by scaled
#

@name xstart

P2F["xstart", string] = "xstart"

function xstart() {
    switch (P["state", number]) {
        case 0,
            if (args() < 1) {
                out("Usage: xstart [program name]\n")
                end()
            }
            elseif (deviceGet("gmod_wire_egp") | deviceGet("gmod_wire_egp_emitter")) {
                local Device = P["device", wirelink]
                
                Device:egpClear()
                Device:egpDrawTopLeft(1)
                Device:egpResolution(vec2(), vec2(512))
                
                Device:egpBox(1, vec2(), vec2())
                
                P["xdevice", wirelink] = Device
                P:remove("device")
                
                P["NULL", table] = NULL
                P["state", number] = 1
            }
            break
        
        case 1,
            if (P["NULL", table] != NULL) {
                local Device = P["xdevice", wirelink]
                
                local Pos0 = Device:egpToWorld(vec2())
                local PosX = Device:egpToWorld(vec2(512, 0))
                local PosY = Device:egpToWorld(vec2(0, 512))
                
                local SizeX = Pos0:distance(PosX)
                local SizeY = Pos0:distance(PosY)
                
                local Width = 512
                if (Device:type() == "gmod_wire_egp_emitter") {
                    Width = 1024
                }
                
                local Res = floor(vec2(SizeX, SizeY) / max(SizeX, SizeY) * Width)
                Device:egpResolution(vec2(), Res)
                
                P["xframe", table] = table(
                    "id" = 1,
                    "type" = "frame",
                    "size" = Res,
                    "count" = 1
                )
                
                P["xobjectsAll", table] = table(P["xframe", table])
                
                local DeviceEntity = Device:entity()
                P["xparam", table] = table(
                    "textMultiplier" = 512 / Res[2],
                    "symbolWidth" = 0.54 * Res[1] / Res[2],
                    "x" = DeviceEntity:toLocalAxis(PosX - Pos0):normalized(),
                    "y" = DeviceEntity:toLocalAxis(PosY - Pos0):normalized(),
                    "z" = DeviceEntity:toLocalAxis((PosY - Pos0):cross(PosX - Pos0)):normalized(),
                    "0" = DeviceEntity:toLocal(Pos0),
                    "dpu" = 512 / SizeX
                )
                
                P["xobjects", table] = table()
                
                P:remove("NULL")
                P["state", number] = 2
            }
            break
        
        case 2,
            P["name", string] = P["args", array]:shiftString()
            P:remove("state")
            break
    }
}
