#
# Created by scaled
#

@name xedit

xmenuAdd("base", "xedit")

PEnabled["xedit", number] = 1

function xedit() {
    switch (P["state", number]) {
        case 0,
            if (xcanCreate(6)) {
                local Res = xres()
                
                xcreate("box"):xsize(Res)
                
                local Size = vec2(Res[1] / 2, 10)
                
                P["button-load", table] = xcreate("box"):xsize(Size):xcolor(vec(191))
                xcreate("text"):xparent(P["button-load", table]):xalign(vec2(1)):xsize(10):xtext("load"):xcolor(vec())
                
                P["button-save", table] = xcreate("box"):xsize(Size):xcolor(vec(191)):xpos(Size:setY(0))
                xcreate("text"):xparent(P["button-save", table]):xalign(vec2(1)):xsize(10):xtext("save"):xcolor(vec())
                
                P["text", table] = xcreate("textLayout"):xsize(Res - vec2(0, 10)):xpos(vec2(0, 10)):xsize(10):xcolor(vec())
                
                P["in", array] = array()
                
                P["state", number] = 1
            }
            break
        
        case 1,
            xcursor()
            local Use = xkeyUse()
            
            if (Use != P["useOld", number]) {
                P["useOld", number] = Use
                
                if (Use) {
                    if (P["button-load", table]:xinrange()) {
                        P["parent", table]["in", array]:pushString("xfilechooser")
                        P["state", number] = 2
                    }
                    elseif (P["button-save", table]:xinrange()) {
                        P["parent", table]["in", array]:pushString("xfilechooser")
                        P["state", number] = 5
                    }
                }
            }
            
            # input
            local Keys = P["kbd", array]
            
            if (Keys:count() > 0 | P["kbdTableOld", table]:count() > 0) {
                local KbdTableOld = P["kbdTableOld", table]
                KbdTableOld:clear()
                P["kbdTableOld", table] = P["kbdTable", table]
                P["kbdTable", table] = KbdTableOld
            }
            
            foreach (I, Key:number = Keys) {
                P["kbdTable", table][Key, number] = 1
            }
            
            if (Keys:count() > 0) {
                for (I = 1, Keys:count()) {
                    local Key = Keys[I, number]
                    if (P["kbdTable", table][Key, number] > P["kbdTableOld", table][Key, number]) {
                        switch (Key) {
                            # Enter
                            case 10, case 13, case 142,
                                P["text", table]:xtext(P["text", table]["text", string] + "\n")
                                break
                            
                            # Backspace
                            case 127,
                                P["text", table]:xtext(P["text", table]["text", string]:sub(1, -2))
                                break
                            
                            # Delete
                            case 148,
                                break
                            
                            # Normal key
                            default,
                                if (inrange(Key, 32, 127)) {
                                    local Char = toChar(Key)
                                    
                                    P["text", table]:xtext(P["text", table]["text", string] + Char)
                                }
                        }
                    }
                }
            }
            break
        
        # load
        case 2,
            if (P["parent", table]["programs", table][1, table]["name", string] == "xfilechooser") {
                P["xfilechooser", table] = P["parent", table]["programs", table][1, table]
                P["xfilechooser", table]["out", array] = P["in", array]
                
                P["state", number] = 3
            }
            break
        
        case 3,
            if (wait()) {
                local Cat = exec("cat")
                Cat["args", array] = array(in())
                Cat["out", array] = P["in", array]
                P["state", number] = 4
            }
            elseif (!P["xfilechooser", table]) {
                P["state", number] = 1
            }
            break
        
        case 4,
            if (wait()) {
                P["text", table]:xtext(in():sub(1, -2))
                P["state", number] = 1
            }
            break
        
        # save
        case 5,
            if (P["parent", table]["programs", table][1, table]["name", string] == "xfilechooser") {
                P["xfilechooser", table] = P["parent", table]["programs", table][1, table]
                P["xfilechooser", table]["out", array] = P["in", array]
                
                P["state", number] = 6
            }
            break
        
        case 6,
            if (wait()) {
                local Touch = exec("touch")
                Touch["args", array] = array(in())
                Touch["in", array] = array(P["text", table]["text", string])
                Touch["out", array] = P["in", array]
                
                P["state", number] = 7
            }
            elseif (!P["xfilechooser", table]) {
                P["state", number] = 1
            }
            break
        
        case 7,
            if (wait()) {
                local In = in()
                if (In != "ok\n") {
                    out(In)
                }
                P["state", number] = 1
            }
            break
    }
}
